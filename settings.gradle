pluginManagement {
	repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		// Currently needed for Intermediary and other temporary dependencies
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		gradlePluginPortal()
	}
}

dependencyResolutionManagement {
	versionCatalogs {
		libs {
			def mc_major = '1.19'
			def mc = mc_major + '.2'
			def mappings = 19

			// // Base // //
			version('minecraft', mc)
			version('ql', "0.17.4")
			version('quiltMappings', "${mc}+build.${mappings}")

			library('minecraft', 'mojang', 'minecraft').versionRef('minecraft')
			library('ql', 'org.quiltmc', 'quilt-loader').versionRef('ql')
			library('quiltMappings', 'org.quiltmc', 'quilt-mappings').versionRef('quiltMappings')

			// // Libraries // //
			version('nbtCrafting', "2.2.1+mc${mc_major}")

			library('nbtCrafting', 'de.siphalor', 'nbtcrafting-' + mc_major).versionRef('nbtCrafting')

		}
	}
}
